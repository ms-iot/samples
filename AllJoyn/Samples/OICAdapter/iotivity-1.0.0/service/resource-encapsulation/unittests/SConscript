#******************************************************************
#
# Copyright 2015 Samsung Electronics All Rights Reserved.
#
#-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
#-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=

##
# ResourceClient Unit Test build script
##
import os
Import('env')

if env.get('RELEASE'):
    env.AppendUnique(CCFLAGS = ['-Os'])
    env.AppendUnique(CPPDEFINES = ['NDEBUG'])
else:
    env.AppendUnique(CCFLAGS = ['-g'])

if env.get('LOGGING'):
    env.AppendUnique(CPPDEFINES = ['TB_LOG'])

# Add third party libraries
lib_env = env.Clone()
SConscript(env.get('SRC_DIR') + '/service/third_party_libs.scons', 'lib_env')

target_os = env.get('TARGET_OS')
if target_os == 'linux':
    # Verify that 'google unit test' library is installed.  If not,
    # get it and install it
    SConscript(env.get('SRC_DIR') + '/extlibs/gtest/SConscript')

    # Verify that 'hippomocks' mocking code is installed.  If not,
    # get it and install it
    SConscript(env.get('SRC_DIR') + '/extlibs/hippomocks.scons')

rcs_test_env = lib_env.Clone()

######################################################################
#unit test setting
######################################################################
src_dir = lib_env.get('SRC_DIR')
gtest_dir = src_dir + '/extlibs/gtest/gtest-1.7.0'

######################################################################
# Build flags
######################################################################
gtest = File(gtest_dir + '/lib/.libs/libgtest.a')
gtest_main = File(gtest_dir + '/lib/.libs/libgtest_main.a')

rcs_test_env.AppendUnique(
        CPPPATH = [
                src_dir + '/extlibs/hippomocks-master',
                src_dir + '/extlibs/gtest/gtest-1.7.0/include',
                '../include',
                '../src/common/utils/include',
        ])

if target_os not in ['windows', 'winrt']:
        rcs_test_env.AppendUnique(CXXFLAGS = ['-std=c++0x', '-Wall'])
        if target_os != 'android':
                rcs_test_env.AppendUnique(CXXFLAGS = ['-pthread'])
                rcs_test_env.AppendUnique(LIBS = ['pthread'])

rcs_test_env.PrependUnique(LIBS = [
    'rcs_client',
    'rcs_server',
    'rcs_common',
    'oc',
    'octbstack',
    'oc_logger',
    'oc_logger_core',
    'connectivity_abstraction',
    gtest,
    gtest_main])

rcs_test_env.AppendUnique(LIBS = ['dl'])

######################################################################
# Build Test
######################################################################
rcs_test_src = env.Glob('./*.cpp')

rcs_client_test = rcs_test_env.Program('rcs_client_test', rcs_test_src)
Alias("rcs_client_test", rcs_client_test)
env.AppendTarget('rcs_client_test')

if env.get('TEST') == '1':
    target_os = env.get('TARGET_OS')
    if target_os == 'linux':
        from tools.scons.RunTest import *
        run_test(rcs_test_env, '', 'service/resource-encapsulation/unittests/rcs_client_test')